import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
  id 'com.android.test'
  id 'org.jetbrains.kotlin.android'
}

android {
  namespace 'codes.chrishorner.socketweather.benchmarks'
  compileSdk versions.compileSdk

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_11
  }

  defaultConfig {
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    benchmark {
      debuggable = true
      signingConfig = signingConfigs.debug
      matchingFallbacks = ['release']
    }
  }

  targetProjectPath = ':app'
  experimentalProperties['android.experimental.self-instrumenting'] = true

  testOptions {
    managedDevices {
      devices {
        pixel6Api31(ManagedVirtualDevice) {
          device = 'Pixel 6'
          apiLevel = 31
          systemImageSource = 'aosp'
        }
      }
    }
  }
}

dependencies {
  implementation deps.androidx.benchmarkJunit4
  implementation deps.androidx.junitRunner
  implementation deps.androidx.uiAutomator
  implementation deps.junit
}

androidComponents {
  beforeVariants(selector().all()) {
    enabled = buildType == 'benchmark'
  }
}
