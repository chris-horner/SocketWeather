apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'app.cash.molecule'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.github.triplet.play'

def versionMajor = 0
def versionMinor = 8
def versionPatch = 0

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId "codes.chrishorner.socketweather"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    debug.java.srcDirs += 'src/debug/kotlin'
    release.java.srcDirs += 'src/release/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
    benchmark.java.srcDirs += 'src/release/kotlin'
  }

  signingConfigs {
    debug {
      storeFile file('keys/debug.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
    }

    release {
      def uploadKey = file('keys/upload.keystore')
      storeFile uploadKey.exists() ? uploadKey : file('keys/debug.keystore')
      storePassword System.getenv("socket_weather_password") ?: 'android'
      keyAlias System.getenv("socket_weather_alias") ?: 'androiddebugkey'
      keyPassword System.getenv("socket_weather_password") ?: 'android'
    }
  }

  buildTypes {
    debug {
      versionNameSuffix '-DEBUG'
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug
    }

    release {
      minifyEnabled true
      shrinkResources true
      signingConfig signingConfigs.release
      proguardFiles file('shrinker_rules.pro')
    }

    benchmark {
      initWith buildTypes.release
      signingConfig signingConfigs.debug
      matchingFallbacks = ['release']
      applicationIdSuffix = '.benchmark'
    }
  }

  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_11
  }

  buildFeatures {
    compose true
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  packagingOptions {
    resources {
      excludes += ['**/*.kotlin_metadata', 'META-INF/*.kotlin_module', 'META-INF/*.properties', '/*.properties']
    }
  }

  bundle {
    language {
      enableSplit = false // We currently only have one language.
    }
  }
  lint {
    disable 'InvalidPackage'
  }
}

play {
  serviceAccountCredentials = file('keys/play_access.json')
  defaultToAppBundles = true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions.freeCompilerArgs += [
    '-Xcontext-receivers',
    '-Xopt-in=kotlin.Experimental',
    '-Xopt-in=kotlinx.coroutines.FlowPreview',
    '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
    '-Xopt-in=kotlin.ExperimentalStdlibApi',
    '-Xopt-in=kotlin.time.ExperimentalTime',
    '-Xopt-in=androidx.compose.material.ExperimentalMaterialApi',
    '-Xopt-in=androidx.compose.animation.ExperimentalAnimationApi'
  ]
}

dependencies {
  implementation deps.accompanist.systemUiController
  implementation deps.androidx.core
  implementation deps.androidx.activityCompose
  implementation deps.androidx.profileInstaller
  implementation deps.androidx.workManager
  implementation deps.compose.material.core
  implementation deps.compose.material.icons
  implementation deps.compose.material.iconsExtended
  implementation deps.compose.ui
  implementation deps.compose.uiGraphics
  implementation deps.compose.uiTooling
  implementation platform(deps.composeBom)
  implementation deps.coroutines.android
  testImplementation deps.coroutines.test
  implementation deps.datastore.core
  debugImplementation deps.datastore.preferences
  debugImplementation (deps.debugDrawer.base) {
    // Remove once this is merged: https://github.com/alorma/Compose-Debug-Drawer/pull/21/files
    exclude group: 'androidx.compose.ui', module: 'ui-test-junit4'
    exclude group: 'androidx.compose.ui', module: 'ui-test-manifest'
  }
  debugImplementation (deps.debugDrawer.modules) {
    exclude group: 'androidx.compose.ui', module: 'ui-test-junit4'
    exclude group: 'androidx.compose.ui', module: 'ui-test-manifest'
  }
  implementation deps.glance
  coreLibraryDesugaring deps.jdkDesugar
  testImplementation deps.junit
  implementation deps.kotlin.reflect
  implementation deps.leakcanary.objectWatcher
  debugImplementation deps.leakcanary.android
  implementation deps.lifecycle
  implementation deps.moshi.core
  implementation deps.moshi.kotlin
  implementation deps.okhttp.core
  debugImplementation deps.okhttp.logging
  testImplementation deps.okhttp.mock
  implementation deps.osmdroid
  implementation deps.retrofit.core
  debugImplementation deps.retrofit.mock
  implementation deps.retrofit.moshi
  testImplementation deps.testParameterInjector
  implementation deps.timber
  testImplementation deps.truth
  testImplementation deps.turbine
  implementation deps.voyager.navigator
  implementation deps.voyager.transitions
}
